grammar sdu.abba.EnvironmentalMonitoring with org.eclipse.xtext.common.Terminals

generate environmentalMonitoring "http://www.abba.sdu/EnvironmentalMonitoring"

Model:
	variables+=Binding*
	machines+=Machine*
;

Machine:
	'machine' name=ID
		sensors+=(Sensor)*
;

Sensor:
	'sensor' (SensorInstantiation | SensorReference)
;

SensorInstantiation:
	name=ID inner=SensorInstantiationInner
;

SensorInstantiationInner:
	{SensorInstantiationInner} 
	'pin' '=' pin=Expression
	settings+=(Setting)+
;

Setting:
	(SamplingRate | BatchSize)
;

SamplingRate:
	'sampling' 'rate' '=' value=Factor ('/'|'per') unit=Unit
;

BatchSize:
	'batch' 'size' '=' value=Expression
	
;

Unit returns Unit:
	{Second} ('s'|'second') | 
	{Minute} ('m'|'minute') | 
	{Hour} ('h'|'hour') | 
	{Day} ('d'|'day')
;

Expression returns Expression:
	Term (({Plus.left=current} '+' | {Subtraction.left=current} '-') right=Term)*
;

Term returns Expression:
	Factor (({Multiplication.left=current} '*' | {Division.left=current} '/') right=Factor)*
;

Factor returns Expression:
	Number | '(' Expression ')' | VariableReference
;

Number:
	value=INT
;

Binding:
	VariableBinding | SensorBinding
;

VariableBinding:
	'var' name=ID '=' (expression=Expression)
;

SensorBinding:
	'var' 'sensor' name=ID '=' (sensor=SensorInstantiationInner)
;

VariableReference returns Expression:
	
	{VariableReference} ref=[VariableBinding]
;

SensorReference:
	'use' ref=[SensorBinding]
;